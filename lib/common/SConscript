# SConscript
#
# Copyright (c) 2015 Kevin Laeufer <kevin.laeufer@rwth-aachen.de>


Import('env')
import os

libname = 'awap-common'
lib_path =  os.path.join(env['AWAP_BUILDPATH'], 'lib', libname)

# tell scons how to generate some files
generated_path = os.path.join(lib_path, 'generated')
generated_files = []

communication = env.GetCommunicationDict()

service_path = os.path.join(generated_path, *("de.rwth_aachen.awap.service".split('.')))
local_service_template = os.path.join('templates', 'AbstractLocalService.java.in')
remote_service_template = os.path.join('templates', 'RemoteService.java.in')
client_interface_template = os.path.join('templates', 'IServiceClient.java.in')
for service in communication['services']:
	local_service = os.path.join(service_path, 'local', "{name}.java".format(**service))
	generated_files += env.Template(
		target=local_service,
		source=local_service_template,
		substitutions={'service': service})
	remote_service = os.path.join(service_path, 'remote', "{name}.java".format(**service))
	generated_files += env.Template(
		target=remote_service,
		source=remote_service_template,
		substitutions={'service': service})
	client_interface = os.path.join(service_path, 'remote', "I{name}Client.java".format(**service))
	generated_files += env.Template(
		target=client_interface,
		source=client_interface_template,
		substitutions={'service': service})

enum_path = os.path.join(generated_path, *("de.rwth_aachen.awap.enums".split('.')))
enum_template = os.path.join('templates', 'Enum.java.in')
for enum in communication['enums']:
	target = os.path.join(enum_path, "{name}.java".format(**enum))
	generated_files += env.Template(
		target=target,
		source=enum_template,
		substitutions={'enum': enum})

msg_path = os.path.join(generated_path, *("de.rwth_aachen.awap.messages".split('.')))
msg_template = os.path.join('templates', 'Message.java.in')
for msg in communication['messages']:
	target = os.path.join(msg_path, msg['service'], "{name}.java".format(**msg))
	generated_files += env.Template(
		target=target,
		source=msg_template,
		substitutions={'msg': msg})

env.Alias("awap-common.generate", generated_files)

# Java sources
source = ['java'] + generated_files

# build ostfriesentee library
base_lib = SConscript([env['OFT_ROOT'] + '/lib/base/SConscript'], exports = 'env')
util_lib = SConscript([env['OFT_ROOT'] + '/lib/util/SConscript'], exports = 'env')
libcommon_oft = env.OstfriesenteeLibrary(libname, source, OFT_LIBS=['base', 'util'])
env.Alias('awap-common.oft', libcommon_oft)
env.Alias('awap-common.size', env.ShowSize(libcommon_oft))

# build library for jade
env['AWAP_LIB_COMMON_JAR'] = os.path.join(lib_path, libname + ".jar")
libcommon_jar = env.JavaToJar(env['AWAP_LIB_COMMON_JAR'], source)
env.Alias('awap-common.jar', libcommon_jar)

Return('libcommon_oft')
